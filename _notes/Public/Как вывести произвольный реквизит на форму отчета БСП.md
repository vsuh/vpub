[EditedByMe:: `= dateformat(date(this.file.mtime), "dd-MM-yyyy HH:mm:ss")`]


В модуле объекта внешнего отчета добавляем процедуры:

- формирования текста версии отчета:
```bsl
Функция ВерсияОбработки()
	Возврат "вер. " + СведенияОВнешнейОбработке().Версия;
КонецФункции

```
- вывода этой строки в реквизит формы с именем `_о_Версия`:
```bsl
Процедура ВывестиВерсиюНаФорму(Форма, ТекстВерсии)

	Если Форма.ТипФормыОтчета <> ТипФормыОтчета.Основная Тогда 
		Возврат;
	КонецЕсли;
	
	
	СписокРеквизитовФормы = Форма.ПолучитьРеквизиты();
	МассивДобавляемыхРеквизитов = Новый Массив;	
	ЭлементыФормы = Форма.Элементы;
	
	МассивДобавляемыхРеквизитов.Добавить(
		Новый РеквизитФормы("_о_Версия"
			, Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(16))
			)
		);
	Форма.ИзменитьРеквизиты(МассивДобавляемыхРеквизитов);
	Форма._о_Версия = ТекстВерсии;

	ГруппаБН = ЭлементыФормы.Найти("БыстрыеНастройки");
	Если ГруппаБН <> Неопределено Тогда 
		ГруппаВерсияСправа = ЭлементыФормы.Добавить("ирГруппаВерсияСправа", Тип("ГруппаФормы"), ГруппаБН);
	Иначе 
		ГруппаВерсияСправа = ЭлементыФормы.Добавить("ирГруппаВерсияСправа", Тип("ГруппаФормы"));
		ЭлементыФормы.Переместить(ГруппаВерсияСправа, Форма);
	КонецЕсли;
	
	ГруппаВерсияСправа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаВерсияСправа.Заголовок = "ГруппаВерсияСправа";
	ГруппаВерсияСправа.ОтображатьЗаголовок = Ложь;	
	ГруппаВерсияСправа.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	ГруппаВерсияСправа.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Право;
	ГруппаВерсияСправа.ВертикальноеПоложениеПодчиненных = ВертикальноеПоложениеЭлемента.Верх;
	ГруппаВерсияСправа.РастягиватьПоГоризонтали = Истина;
	ГруппаВерсияСправа.РастягиватьПоВертикали = Ложь;

	НовыйРеквизит = ЭлементыФормы.Добавить("ВерсияОбработки", Тип("ПолеФормы"), ГруппаВерсияСправа);
	НовыйРеквизит.Заголовок = "";
	НовыйРеквизит.ПутьКДанным = "_о_Версия";
	НовыйРеквизит.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйРеквизит.Видимость = Истина;
	НовыйРеквизит.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	НовыйРеквизит.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	НовыйРеквизит.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовыйРеквизит.РастягиватьПоГоризонтали = Истина;	
	НовыйРеквизит.Шрифт = Новый Шрифт(НовыйРеквизит.Шрифт,,8);

	//  для визуализации контейнера на форме
	//  ГруппаВерсияСправа.ЦветФона =  Новый Цвет(233,233, 222);
	НовыйРеквизит.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки, 1);
КонецПроцедуры

```

- определяем, что хотим обработать событие __ПослеЗаполненияПанелиБыстрыхНастроек__ внешнего отчета:

> _это последнее из 10 событий, возникающих при открытии общей формы отчета **БСП**_

```bsl
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
    Настройки.ФормироватьСразу = Ложь;
    Настройки.РазрешеноИзменятьВарианты = Истина;
	Настройки.СкрытьКомандыРассылки = Истина;

	Настройки.События.ПослеЗаполненияПанелиБыстрыхНастроек = Истина;
	
КонецПроцедуры
```

- обрабатываем событие __ПослеЗаполненияПанелиБыстрыхНастроек__ внешнего отчета:
```bsl

Процедура ПослеЗаполненияПанелиБыстрыхНастроек(ФормаОтчета, ПараметрыЗаполнения) Экспорт
	ВывестиВерсиюНаФорму(ФормаОтчета, ВерсияОбработки());
КонецПроцедуры
```

##### все обработчики событий формы в порядке возникновения:

- ПриОпределенииИспользуемыхТаблиц
```bsl
Процедура ПриОпределенииИспользуемыхТаблиц**(КлючТекущегоВарианта, ИспользуемыеТаблицы) 
```

- ПриСозданииНаСервере
```bsl
Процедура **ПриСозданииНаСервере**(ФормаОтчета, Отказ, СтандартнаяОбработка) 
```
- ПередЗагрузкойВариантаНаСервере
```bsl
Процедура **ПередЗагрузкойВариантаНаСервере**(ФормаОтчета, НастройкиКД) 
```
- ПриЗагрузкеВариантаНаСервере
```bsl
Процедура **ПриЗагрузкеВариантаНаСервере(ФормаОтчета, НастройкиКД) 
```
- ПриЗагрузкеПользовательскихНастроекНаСервере
```bsl
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(ФормаОтчета, НовыеПользовательскиеНастройкиКД)
```
- ПередЗаполнениемПанелиБыстрыхНастроек
```bsl
Процедура ПередЗаполнениемПанелиБыстрыхНастроек(ФормаОтчета, ПараметрыЗаполнения) 
```
- ПередЗагрузкойНастроекВКомпоновщик
```bsl
Процедура ПередЗагрузкойНастроекВКомпоновщик(ФормаОтчета,
			КлючСКД,
 			КлючТекущегоВарианта,
 			НовыеНастройкиКД,
 			НовыеПользовательскиеНастройкиКД) 
```
- ДополнитьСвязиОбъектовМетаданных
```bsl
Процедура ДополнитьСвязиОбъектовМетаданных(ТаблицаСвязей) 
```
- ПриОпределенииПараметровВыбора
```bsl
Процедура ПриОпределенииПараметровВыбора(ПустойПараметр, НастройкаВарианта) 
```
- ПослеЗаполненияПанелиБыстрыхНастроек
```bsl
Процедура ПослеЗаполненияПанелиБыстрыхНастроек(ФормаОтчета, ПараметрыЗаполнения) 
```